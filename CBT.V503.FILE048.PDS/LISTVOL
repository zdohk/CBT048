*          DATA SET CBT500     AT LEVEL 001 AS OF 06/26/86
         TITLE 'LISTVOL - DOCUMENTATION'                                00001
*.....................................................................* 00002
*.                                                                   .* 00003
*.   LISTVOL                                                         .* 00004
*.                                                                   .* 00005
*        LISTVOL   LEVEL('DSNAME')|DA('DSLIST')                       * 00006
*                  VOL('SER')                                         * 00007
*        'DSNAME' IS EITHER HIGHER LEVEL QUALIFIERS OF CATALOG        * 00008
*                 STRUCTURE OR CONTAINS EMBEDDED ASTERISK AS          * 00009
*                 PART OF THE NAME.                                   * 00010
*        'DSLIST' IS LIST OF DATASETS.                                * 00011
*        'SER'    IS LEADING CHARACTERS OF VOLUME SERIAL. ONLY        * 00012
*                 DATASETS CATALOGED ON VOLUMES BEGINNING THUSLY      * 00013
*                 WILL BE LISTED.                                     * 00014
*              NOW THIS CODE IS BORROWED FROM THE LISTS COMMAND FROM  * 00015
*              SHARE, AND IS A VERY MUCH STRIPPED DOWN VERSION OF     * 00016
*              THAT COMMAND.                                          * 00017
*              THE REAL WORK IS DONE IN SUBROUTINE LOCINDEX, AND SINCE* 00018
*              I WROTE THAT MYSELF, I SUGGEST YOU LOOK THERE FOR      * 00019
*              INSIGHT INTO MVS CATALOG MANAGEMENT.                   * 00020
*        LEVEL CURRENT TO ALL TSO SUBJECT TO WHICH LOCINDEX YOU HAVE  * 00021
*                                                                     * 00022
*********************************************************************** 00023
* 06/13/84 - ADDED SUPPORT FOR MULTI-VOLUME DATASETS       LMB-CBT    * 00024
*                                                                     * 00025
* 06/25/86 - MSG TO USE LISTC OR DSAT COMMAND IF LOCINDEX  LMB-CBT    * 00026
*            HAS INSUFFICIENT STORAGE FOR PROCESSING       LMB-CBT    * 00027
*            (RETURN CODE 40 OR 44 FROM SVC 26)            LMB-CBT    * 00028
* 01/03/06 - ADDED SPACE IN GETMAIN, CONVERT TPUT TO PUTLINE     SBG  * 00009
*.....................................................................* 00029
         MACRO
&LABEL   APUT  &MSG,                    @MESSAGE TEXT                  &
               &LEN,                    @LENGTH OF MESSAGE             &
               &RSA=,                   OPTIONAL 2 WORD SAVE AREA      &
               &EPA=                    EPUTL EPA
.*
.* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
.*
.*  --> E@PUT
.*
.*  THIS MACRO SETS UP A PARAMETERS AND INVOKES THE EPUTL ROUTINE.
.*
.*
.*  ARGUMENTS: -----------------------------------------------------
.*
.*      &MSG : ADDRESS OF MESSAGE TEXT OR A LITERAL
.*
.*      &LEN : LENGTH OF MESSAGE TEXT (DEFAULT IS L'&MSG)
.*
.*      &RSA :(OPTIONAL) 1 WORD SAVE AREA TO PRESERVE R14 FOR TPUT
.*            COMPATIBILITY.
.*
.*      &EPA :(OPTIONAL) EPA FOR EPUTL ROUTINE
.*
.*
.*   EXAMPLES: -----------------------------------------------------
.*
.*      1) APUT  'LITERAL'
.*
.*      2) APUT  (R3),80
.*
.*      3) APUT  LABEL
.*
.*      4) APUT  LABEL,40
.*
.*
.* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
.*
.*
.*  LOCAL SET SYMBOLS
.*
         LCLC  &SMA                     MESSAGE ADDRESS (REG)
         LCLC  &SML                     MESSAGE LENGTH (REG)
         LCLC  &SEP                     EPA (REGISTER)
.*
         LCLC  &L1,&L2,&L3              LOCAL LABELS
.*
&L1      SETC  'EP'.'&SYSNDX'.'L1'      LOCAL LABELS
&L2      SETC  'EP'.'&SYSNDX'.'L2'      LOCAL LABELS
&L3      SETC  'EP'.'&SYSNDX'.'L3'      LOCAL LABELS
.*
.*
.*  MESSAGE ADDRESS
.*
         AIF   (T'&LABEL EQ 'O').A0500                      *HMD 11/86*
.*                                                          *HMD 11/86*
&LABEL   DS    0H                                           *HMD 11/86*
.*                                                          *HMD 11/86*
.A0500   ANOP                                               *HMD 11/86*
         AIF   ('&MSG' NE '').A2000
         MNOTE 8,'''&MSG='' MISSING / REQUIRED'
         MEXIT
.*
.A2000   ANOP
         AIF   ('&MSG'(1,1) NE '(').A2400
&SMA     SETC  '&MSG'(2,K'&MSG-2)
         LR    R1,&SMA                  @MESSAGE TEXT
         AGO   .A2600
.*
.A2400   ANOP
         AIF   ('&MSG'(1,1) NE '''').A2500
         B     &L3                      BRANCH AROUND MESSAGE
&L2      DC    C&MSG
&L3      DS    0H
         LA    R1,&L2                   @MESSAGE
         AGO   .A2600
.*
.A2500   ANOP
         LA    R1,&MSG                  @MESSAGE TEXT
.A2600   ANOP
.*
.*  MESSAGE LENGTH
.*
         AIF   ('&LEN' NE '').A3000
         AIF   ('&MSG'(1,1) NE '''').A2800
         LA    R0,L'&L2                 MESSAGE LENGTH
         AGO   .A3600
.*
.A2800   ANOP
         AIF   ('&MSG'(1,1) EQ '(').A2900
         LA    R0,L'&MSG                MESSAGE LENGTH
         AGO   .A3600
.*
.A2900   ANOP
         MNOTE 8,'''LEN'' MISSING / REQUIRED'
         MEXIT
.*
.A3000   ANOP
         AIF   ('&LEN'(1,1) NE '(').A3400
&SML     SETC  '&LEN'(2,K'&LEN-2)
         LR    R0,&SML                  MESSAGE LENGTH
         AGO   .A3600
.*
.A3400   ANOP
         LA    R0,&LEN                  MESSAGE LENGTH
.A3600   ANOP
.*
.*  RSA
.*
         AIF   ('&RSA' EQ '').A4900
         ST    R14,&RSA                 SAVE REGS 14
.A4900   ANOP
.*
.*  ENTRY POINT
.*
         AIF   ('&EPA' NE '').A5000
         L     R15,=V(EPUTL)            EPA
         AGO   .A5600
.*
.A5000   AIF   ('&EPA'(1,1) NE '(').A5400
&SEP     SETC  '&EPA'(2,K'&EPA-2)
         LR    R15,&SEP                 EPA
         AGO   .A5600
.*
.A5400   ANOP
         L     R15,&EPA                 EPA
.*
.A5600   ANOP
.*
.*  CALL LINKAGE
.*
.ACALL   BALR  R14,R15                  CALL PUTLINE ROUTINE
.*
         AIF   ('&RSA' EQ '').A6900
         L     R14,&RSA                 RESTORE R14
.A6900   ANOP
.*
.AMEND   MEND
         TITLE 'LISTVOL - LIST VOLUME COMMAND'                          00030
LISTVOL  START 0                                                        00031
         STM   R14,R12,12(R13)                                          00032
         USING LISTVOL,R15                                              00033
         ST    R13,SAVE+4                                               00034
         LA    R13,SAVE                                                 00035
         B     BEGIN                                                    00036
SAVE     DC    18F'0'                                                   00037
         DROP  R15                                                      00038
         USING SAVE,R13                                                 00039
         TITLE 'LISTVOL - GETMAIN AND PARSE'                            00040
BEGIN    LR    R4,R1                                                    00041
         USING CPPL,R4                                                  00042
         LA    R0,LWORK            GET LENGTH OF WORK AREA              00043
         A     R0,=F'4096'         GET EXTRA 4K FOR LOCINDEX WORK   SBG 00044
         ICM   R0,B'1000',=FL1'1'  SUBPOOL 1                            00045
         GETMAIN R,LV=(0)                                               00046
         LR    R12,R1               SAVE ADDR OF WORK AREA              00047
         USING WORKAREA,R12                                             00048
         LA    R0,LOCINDXW          POINT TO LOCINDEX WORK AREA         00049
         ST    R0,PARM+12           SAVE ADDR IN PARM LIST              00050
         MVI   PARM+12,X'80'        INDICATE END OF PARM LIST           00051
         MVI   WXTNT+4,X'04'                                            00052
         LA    R3,WPPL             GET ADDR OF PPL                      00053
         USING PPL,R3                                                   00054
         L     R0,CPPLCBUF                                              00055
         ST    R0,PPLCBUF          SAVE ADDR OF COMMAND BUFFER          00056
         L     R0,CPPLUPT                                               00057
         ST    R0,PPLUPT           SAVE ADDR OF UPT                     00058
         L     R0,CPPLPSCB                                              00059
         LR    R10,R0              GET ADDR OF USERID FROM PSCB         00060
         SR    R11,R11                                                  00061
         IC    R11,7(R10)          GET LENGTH OF USERID                 00062
         BCTR  R11,0                                                    00063
AUTHRZD  OI    FLAG,X'80'                                               00064
LECT     L     R0,CPPLECT                                               00065
         ST    R0,PPLECT           SAVE ADDR OF ECT                     00066
         LA    R0,CPECB                                                 00067
         ST    R0,PPLECB           SAVE ADDR OF ECB                     00068
         XC    CPECB,CPECB         CLEAR ECB                            00069
         L     R0,=A(PARSLIST)                                          00070
         ST    R0,PPLPCL           SAVE ADDR OF PCL                     00071
         LA    R0,WPDL                                                  00072
         ST    R0,PPLANS           SAVE ADDR OF PDL AREA                00073
         XC    PPLUWA,PPLUWA       CLEAR UWA                            00074
         LR    R1,R3               PASS ADDR OF PPL TO PARSE            00075
         LINK  EP=IKJPARS          LINK TO PARSE SERVICE ROUTINE        00076
         LTR   R15,R15             PARSE ERROR                          00077
         BNZ   RETC                 YES                                 00078
         TM    PPLANS,X'FF'        PARSE ERROR                          00079
         BO    RETC                 YES                                 00080
         DROP  R3,R4                                                    00081
         L     R3,WPDL             GET ADDR OF PDL                      00082
         USING IKJPARMD,R3                                              00083
         TM    EXTENT+1,X'01'      EXTENT SPECIFIED?                    00084
         BZ    *+8                  NO                                  00085
         OI    FLAG,X'20'           YES, SET FLAG BIT                   00086
         TM    DSNLIST+6,X'80'      DSNAME SPECIFIED?                   00087
         BO    DSNRTN                YES                                00088
         TM    LEV+6,X'80'          INDEX LEVEL SPECIFIED?              00089
         BO    LEVEL                 YES                                00090
         B     EUSLO                NO PARMS - LISTS FOR USERID         00091
         TITLE 'LISTVOL - DATASET NAME ROUTINE'                         00092
DSNRTN   LA    R4,DSNLIST           POINT TO DSN LIST                   00093
*        LA    R0,L'MSG1                                                00094
*        LA    R1,MSG1                                                  00095
*        SVC   93                  TPUT HEADING                         00096
         APUT  MSG1,L'MSG1
DSNAME   L     R5,0(R4)             POINT TO DSNAME                     00097
         LH    R6,4(R4)             GET LENGTH OF DSNAME                00098
         BCTR  R6,0                                                     00099
         MVI   DSN1,X'40'                                               00100
         MVC   DSN1+1(43),DSN1     BLANK DSN WORK AREA                  00101
         LA    R8,DSN1              POINT TO DSN WORK AREA              00102
         TM    6(R4),X'40'          DSNAME CONTAINED IN QUOTES?         00103
         BO    MVCDSNAM               YES                               00104
         EX    R11,USIDMVC         MOVE USERID TO WORK AREA             00105
         LA    R8,1(R11,R8)        BUMP PAST USERID                     00106
         MVI   0(R8),C'.'          MOVE PERIOD TO WORK AREA             00107
         LA    R8,1(0,R8)          BUMP PAST                            00108
MVCDSNAM EX    R6,DSNMVC           MOVE DSNAME TO WORK AREA             00109
         MVC   VOLS,BLANKS         MOVE BLANKS TO VOLSER WORK AREA      00110
         TM    FLAG,X'80'          AUTHORIZED USER?                     00111
         BZ    LOCDSN               NO                                  00112
         TM    VOL+6,X'80'         VOLUME SPECIFIED?                    00113
         BZ    LOCDSN               NO, LOCATE DSN                      00114
         LA    R1,VOLS             POINT TO VOLSER WORK AREA            00115
         L     R5,VOL              POINT TO VOLUME PARAMETER            00116
         LH    R6,VOL+4            GET LENGTH OF VOL PARM               00117
         BCTR  R6,0                                                     00118
         EX    R6,MVCVOLS          MOVE VOL PARM TO VOLSER WORK AREA    00119
         B     BALOBTN             BYPASS LOCATE                        00120
LOCDSN   OI    FLAG,X'10'                                               00121
         LOCATE INDS                                                    00122
         LTR   R15,R15              DID IT FIND VOL SER                 00123
         BNZ   ERR03                 NO WRITE ERROR MSG                 00124
         NI    FLAG,X'EF'                                               00125
         LH    R7,BUF1             2 BYTE - # OF VOLS RETURNED  LMB-CBT 00126
*        MVC   VOLS(6),BUF1+6      YES - MOVE IN VOL SER                00127
         LA    R9,BUF1             ADDR OF RETURNED VOLUME LIST LMB-CBT 00128
         LA    R9,2(R9)            1ST 12 BYTE VARIABLE SECTION LMB-CBT 00129
         LA    R9,4(R9)            VOL=SER LOCATION             LMB-CBT 00130
MULTIVOL MVC   VOLS(6),0(R9)       MOVE IN VOL SER              LMB-CBT 00131
         SPACE 2                                                        00132
BALOBTN  TM    VOL+6,X'80'     WAS VOL PARM GIVEN?              LMB-CBT 00133
         BZ    BALOBTN2        NO, SO TPUT IT                   LMB-CBT 00134
         LH    R1,LENVOLS      LENGTH OF VOLS                   LMB-CBT 00135
         EX    R1,CLCVOLS      COMPARE THE VOLS                 LMB-CBT 00136
         BNE   CONTVAR         NO HIT, CONTINUE THE VARIABLES   LMB-CBT 00137
BALOBTN2 APUT  VOLS,52                                                  00138
CONTVAR  LA    R9,12(R9)           PT TO NEXT VARIABLE SECTION  LMB-CBT 00139
         BCT   R7,MULTIVOL         MORE VOLS-GO PROCESS         LMB-CBT 00140
RETDSN   TM    MULTISW,X'01' WAS MULTI-VOL RECVD FROM LOCINDEX?   LMB   00141
         BC    8,RETDSN2        NO, SO CONTINUE WITH DA(...) LIST LMB   00142
         MVI   MULTISW,X'00'    REINITIALIZE                      LMB   00143
         B     LOOP             CONTINUE WITH LOCINDEX PROCESSING LMB   00144
RETDSN2  L     R4,24(R4)           GET DSN LINK POINTER                 00145
         C     R4,ENDLIST          END OF LIST?                         00146
         BE    RETC                 YES                                 00147
         B     DSNAME               NO, GET NEXT DSNAME                 00148
         TITLE 'LISTVOL - LEVEL ROUTINE'                                00149
EUSLO    EQU   *                                                        00150
**     FOR ENTIRE USERID LOGGED ON                                      00151
         MVC   USERID(7),0(R10)        MOVE IN USERID                   00152
         B     GOTUS               BRANCH TO GOT USERID                 00153
         SPACE 3                                                        00154
LEVEL    EQU   *                                                        00155
         L     R5,LEV              POINT TO INDEX STRUCTURE             00156
         LH    R6,LEV+4            GET LENGTH OF INDEX STRUCT           00157
         BCTR  R6,0                                                     00158
         LA    R8,USERID           POINT TO WORK AREA                   00159
         EX    R6,DSNMVC           MOVE INDEX STRUCTURE TO WORK AREA    00160
GOTUS    EQU   *                                                        00161
         TM    VOL+8,X'80'                                              00162
         BZ    NVOLL                                                    00163
         LA    R1,VOLZ                                                  00164
         L     R5,VOL              POINT TO VOLUME PARAMETER            00165
         LH    R6,VOL+4            GET LENGTH OF VOL PARM               00166
         BCTR  R6,0                                                     00167
         STH   R6,LENVOLS                                               00168
         EX    R6,MVCVOLS                                               00169
NVOLL    DS    0H                                                       00170
*        LA    R0,L'MSG1                                                00170
*        LA    R1,MSG1                                                  00171
*        SVC   93                  TPUT HEADING                         00172
         APUT  MSG1,L'MSG1
         OI    FLAG,X'40'          SET FLAG FOR DATA SETS FOR USERID    00173
LOOP     EQU   *                   LOOP FOR DATASET IN USERID           00174
         L     R15,=V(LOCINDEX)    POINT TO EP(LOCINDEX)                00175
         LA    R1,PARM             POINT TO PARM LIST                   00176
         BALR  R14,R15             GO TO LOCINDEX                       00177
         CH    R15,=H'18'          WAS RC=0018?                         00178
         BE    MULTI               YES, IT IS TAPE WITH MULTI-VOLS      00179
         CH    R15,=H'40'          WAS RC=X'0028' ?                     00180
         BE    INSUFSTO            YES, THERE WAS INSUFFICIENT STORAGE  00181
         CH    R15,=H'44'          WAS RC=X'002C' ?                     00182
         BE    INSUFSTO            YES, THERE WAS INSUFFICIENT STORAGE  00183
         B     *+4(R15)            BRANCH , DEP ON RETURN CODE          00184
         B     OK                                                       00185
         B     NOUSER              4-PRINT MSG & RETRUN TO SYS          00186
         B     OK                  8-                                   00187
         B     MULTI               12-DS IS ON MULTI-VOLUMES            00188
         B     RETC                16-FINISHED-RETURN TO SYS            00189
         B     RDERR               20-PRINT MSG&RETRUN TO SUS           00190
MULTI    MVI   MULTISW,X'01'       SET SWITCH SAYING MULTI-VOL FOUND    00191
         B     LOCDSN                                                   00192
OK       TM    VOL+6,X'80'                                              00193
         BZ    NVOL2                                                    00194
         LH    R1,LENVOLS                                               00195
         EX    R1,CLCVOLS                                               00196
         BNE   LOOP                                                     00197
NVOL2    APUT  VOLS,52                                                  00198
         B     LOOP                GET NEXT DSNAME FROM LOCINDEX        00199
         SPACE 3                                                        00200
         TITLE 'LISTVOL - EOJ AND ERROR ROUTINES'                       00201
RETC     EQU   *                                                        00202
         L     13,SAVE+4           LOAD R13 PREVIOUS SPACE AREA         00203
         LM    2,12,28(13)         RELOAD REGISTERS                     00204
         L     14,12(13)           LOAD RETURN ADDRESS                  00205
         MVI   12(13),X'FF'        INDICATE CONTROL RETURN CALLING PROG 00206
         BCR   15,14               RETURN TO CALLING PROGRAM            00207
         SPACE 3                                                        00208
ERR01    EQU   *                                                        00209
* NO VALID COMMAND                                                      00210
         APUT  ERMSG1,18                                                00211
         B     RETC                BRANCH TO RETURN CODE                00212
* DATASET NAME NOT FOUND                                                00213
ERR03    MVC   MSG2(L'ERMSG9),ERMSG9                                    00214
         MVC   MSG2+L'ERMSG9(44),DSN1                                   00215
*        LA    R0,L'ERMSG9+44                                           00216
*        LA    R1,MSG2                                                  00217
*        SVC   93                                                       00218
         APUT  MSG2,L'ERMSG9+44
         TM    FLAG,X'40'                                               00219
         BO    LOOP                                                     00220
         TM    FLAG,X'10'                                               00221
         B     RETDSN                                                   00222
         SPACE 3                                                        00223
RDERR    MVC   MSG2(L'ERMSG8),ERMSG8                                    00224
         MVC   MSG2+L'ERMSG8(44),DSN1                                   00225
*        LA    R0,L'ERMSG8+44                                           00226
*        LA    R1,MSG2                                                  00227
*        SVC   93                                                       00228
         APUT  MSG2,L'ERMSG8+44
         B     LOOP                CONTINUE THRU LOOP                   00229
         SPACE 3                                                        00230
NOUSER   MVC   MSG2(L'ERMSG3),ERMSG3                                    00231
         MVC   MSG2+L'ERMSG3(44),USERID                                 00232
*        LA    R0,L'ERMSG3+44                                           00233
*        LA    R1,MSG2                                                  00234
*        SVC   93                                                       00235
         APUT  MSG2,L'ERMSG3+44
         B     RETC                RETURN TO SYSTEM                     00236
         SPACE 3                                                        00237
INSUFSTO MVC   MSG2(L'ERMSG11),ERMSG11                                  00238
*        LA    R0,L'ERMSG11                                             00239
*        LA    R1,MSG2                                                  00240
*        SVC   93                                                       00241
         APUT  MSG2,L'ERMSG11
         B     RETC                RETURN TO SYSTEM                     00242
         SPACE 3                                                        00243
ERRF     EQU   *                                                        00244
         APUT  ERMSG10,20                                               00245
         B     RETC                                                     00246
USIDMVC  MVC   0(0,R8),0(R10)                                           00247
DSNMVC   MVC   0(0,R8),0(R5)                                            00248
MVCVOLS  MVC   0(0,R1),0(R5)                                            00249
CLCVOLS  CLC   VOLS(0),VOLZ                                             00250
         TITLE 'LISTVOL - DATA AREAS'                                   00251
ENDLIST  DS    0F                                                       00252
         DC    XL4'FF000000'                                            00253
* SETUP CONSTANTS                                                       00254
VOLDSCTK DC    H'0'                NUM DSCH ON A TRK                    00255
VOLF4CHR DC    XL5'00'             CCHHR OF DSCB                        00256
VOLNO    DC    H'0'                NUM VOLUMES PROCESSED                00257
PEXCTR   DC    H'0'                CTR FOR TATAL NUM FREE SPACE         00258
LASTTRK  DC    H'0'                                                     00259
VOLS     DC    CL6' '                 VOLUME SER                        00260
         DC    C'  '                                                    00261
DSN1     DC    CL44' '              DATASET NAME                        00262
USERID   DC    CL44' '              USERID                              00263
FLAG     DC    X'00'                                                    00264
DSORG    DC    X'00'                                                    00265
BLANKS   DC    CL6' '                                                   00266
VOLZ     DC    CL6' '                                                   00267
LENVOLS  DC    H'0'                                                     00268
BUF1     DS    0D                                                       00269
         DS    265C                                                     00270
TRTBL    EQU   *-240                                                    00271
         DC    C'0123456789ABCDEF'                                      00272
MASKED   DC    XL6'402020202020'   MASK FIELD FOR EDIT                  00273
         SPACE 3                                                        00274
* MESSAGE TO BE PRINTED                                                 00275
MSG1     DC    C'VOLUME  DATASET NAME             '                     00276
MSG2     DC    CL76' '                                                  00277
MSG4     DC    C'VOLUME  DATASET NAME             '                     00278
         SPACE 3                                                        00279
* ERROR MESSAGES                                                        00280
ERMSG1   DC    C'NO VALID COMMAND'                                      00281
*ERMSG3   DC    C'INVALID LEVEL - '                                     00282
ERMSG3   DC    C'NO DATASETS CATALOGED TO - '                           00283
ERMSG8   DC    C'ERROR READING CATALOG DSN - '                          00284
ERMSG9   DC    C'DSN NOT FOUND - '                                      00285
ERMSG10  DC    C'UNSUPPORTED KEYWORD'                                   00286
ERMSG11  DC    C'INSUFFICIENT STORAGE USE LISTC OR DSAT COMMAND'        00287
         LTORG                                                          00288
         EJECT                                                          00289
* CAMLST FOR DSCB3                                                      00290
SEEKCAM  CAMLST SEEK,VTOCCHHR,VOLS,BUF1                                 00291
         SPACE 2                                                        00292
* CAMLST LOCATE DSN OV VOL SER                                          00293
INDS     CAMLST NAME,DSN1,,BUF1                                         00294
         SPACE 2                                                        00295
* CAMLST FOR DSCB 1                                                     00296
SERCHCAM CAMLST SEARCH,DSN1,VOLS,BUF1                                   00297
         EJECT                                                          00298
* CONSTANTS AND WORK AREAS                                              00299
HWK1     DC    H'0'                HALF WORD WORK AREA                  00300
VOLTKCYL DC    H'19'               # TRKS/CYL                           00301
VTOCCHHR DC    XL5'0'              TRACK ADDR WORK AREA                 00302
WKD      DC    D'0'                DOUBLE WORK WORK AREA                00303
         SPACE 3                                                        00304
* PARM SETUP FOR GETDSN                                                 00305
PARM     DC    A(USERID)           USERID ADDRESS                       00306
         DC    A(DSN1)             DSN ADDRESS                          00307
         DC    A(VOLS)             VOL SER ADDRESS                      00308
         DC    A(0)                ADDRESS FOR GETMAIN                  00309
         SPACE 3                                                        00310
* EQUATE REGISTERS                                                      00311
         YREGS                                                          00312
RCC      EQU   R8                                                       00313
RHH      EQU   R7                                                       00314
RR       EQU   R6                                                       00315
         TITLE 'LISTVOL - PARSE PARAMETER LIST'                         00316
PARSLIST IKJPARM                                                        00317
DSNKYD   IKJKEYWD                                                       00318
         IKJNAME 'DATASET',SUBFLD=DSNSUBF                               00319
LEVL     IKJKEYWD                                                       00320
         IKJNAME 'LEVEL',SUBFLD=LVL                                     00321
VOLUME   IKJKEYWD                                                       00322
         IKJNAME 'VOLUME',SUBFLD=VLM                                    00323
EXTENT   IKJKEYWD                                                       00324
         IKJNAME 'EXTENTS'                                              00325
DSNSUBF  IKJSUBF                                                        00326
DSNLIST  IKJPOSIT DSNAME,LIST                                           00327
LVL      IKJSUBF                                                        00328
LEV      IKJIDENT 'LEVEL',OTHER=ANY,MAXLNTH=44                          00329
VLM      IKJSUBF                                                        00330
VOL      IKJIDENT 'VOLUME',LIST,FIRST=ALPHANUM,OTHER=ALPHANUM,MAXLNTH=6 00331
         IKJENDP                                                        00332
         IKJPPL                                                         00333
         IKJCPPL                                                        00334
         TITLE 'LISTVOL - WORK AREA'                                    00335
WORKAREA DSECT                                                          00336
MULTISW  DS    X      SW FOR MULTI-VOLS FROM LOCINDEX          LMB      00337
WPPL     DS    7A                                                       00338
CPECB    DS    F                                                        00339
WPDL     DS    F                                                        00340
OBTNSAVE DS    10F                                                      00341
XTNTS    DS    16XL10                                                   00342
WXTNT    DS    CL5                                                      00343
         DS    0D                                                       00344
LOCINDXW EQU   *                                                        00345
LWORK    EQU   *-WORKAREA                                               00346
#PAN$AUD CSECT                                                          003464
$PAN#AUD DC    CL21'001CBT500    06/26/86'                              003465
         END                                                            00347
EPUTL    TITLE 'PUTLINE FOR FEWER THAN 83 ARGUMENTS IN THE KEY OF C MAJ&
               OR'
EPUTL    CSECT                                              *HMD 10/85*
         LCLB  &XA                                          *HMD 10/85*
&XA      SETB  1                  SET TO ZERO ON NON/XA     *HMD 10/85*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*  MODULE NAME  . . . . EPUTL
*
*  ALIASES  . . . . . . NONE
*
*  DATE WRITTEN . . . . 06/85
*  UPDATED      . . . . 04/88                                  HD APR88
*
*  AUTHOR . . . . . . . JIM ELSWORTH
*  UPDATOR  . . . . . . HOWARD M. DEAN                         HD APR88
*
*  ENVIRONMENT  . . . . TSO COMMAND-PROCESSOR
*
*  PURPOSE:
*    ALLOW AN EASY INTERFACE TO PUTLINE ROUTINE
*
*
*  DESCRIPTION:
*    BUILDS PUTLINE PARAMETER LIST BASED ON INPUT ARGUMENTS.
*    ISSUES SINGLE LINE PUTLINE.
*
*
*  ATTRIBUTES:
*    REENTRANT
*
*  FEATURES:
*    WILL RUN UNDER BATCH TMP SINCE PUTLINE USED INSTEAD OF TPUT.
*
*  CALLED MODULES:
*    PUTLINE
*
*  ASSUMPTIONS:
*    CALLED BY A TSO-CP THAT IS OBSERVING STANDARD LINKAGE
*
*  ABENDS:
*    POSSIBLE 0CX IF BAD ARGUEMNTS
*
*  CAVEATS:
*    UNPREDICTABLE RESULTS IF FIRST PROGRAM RUNNING UNDER THIS TCB
*    DID NOT SAVE (14,12) BEFOR R1 WAS MODIFIED.
*
*
*  INPUT ARGUMENTS:
*    1)  R1 : ADDRESS OF MESSAGE TEXT
*    2)  R0 : LENGTH OF MESSAGE TEXT
*    3) R13 : STANDARD OS SAVE AREA
*    4) R14 : RETURN ADDRESS
*    5) R15 : ENTRY ADDRESS
*
*               ( CONTINUED ... )
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*
*  OUTPUT ARGUMENT: RETURN CODE FROM PUTLINE OR
*
RCBA     EQU   100                 BAD MESSAGE ADDRESS
RCBL     EQU   104                 MESSAGE LENGTH <1 OR >MAXMSG
*
MAXMSG   EQU   128                 MAXIMUM MESSAGE LENGTH
*
*
*  REGISTERS USED:
*
R0       EQU   0                   MESSAGE LENGTH AT ENTRY
R1       EQU   1                   MESSAGE ADDRESS AT ENTRY
R2       EQU   2                   WORK
RMA      EQU   4                   @MESSAGE TEXT
RML      EQU   5                   MESSAGE LENGTH
RUPT     EQU   6                   @UPT (USER PROFILE TABLE)
RECT     EQU   7                   @ECT
R12      EQU   12                  CSECT BASE
R13      EQU   13                  @SAVE AREA AND DYNAMIC WORK AREA
R14      EQU   14                  WORK/LINKAGE
R15      EQU   15                  WORK/LINKAGE
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
         IKJCPPL
         EJECT
         IKJTCB   LIST=YES                                  *HMD 10/85*
         EJECT                                              *HMD 10/85*
         CVT      DSECT=YES,LIST=YES                        *HMD 10/85*
         EJECT
         IHAPSA   LIST=YES                                     HD APR88
         EJECT                                                 HD APR88
PUTLDS   DSECT
         DS    18F                      OS SAVE AREA
PUTAAA   EQU   *
PUTECB   DS    F                        ECB
PUTIOPL  DS    4F                       IOPLADS
PUTBLOK  PUTLINE MF=L
PUTLEN   DS    H                        LENGTH
PUTZZZ   DS    H                        RESERVED
PUTMSG   DS    (MAXMSG)C                MESSAGE TEXT AREA
PUTSIZE  EQU   *-PUTLDS                 SIZE OF PUTLINE PARMS
         EJECT
***********************************************************************
*  SET UP LINKAGE
***********************************************************************
         SPACE 1
EPUTL    CSECT
         AIF  (NOT &XA).X001                                *HMD 10/85*
EPUTL    AMODE 31
.X001    ANOP                                               *HMD 10/85*
         STM   14,12,12(13)       SAVE CALLERS REGS
         LR    R12,R15            COPY EPA
         USING EPUTL,R12          ADDRESSABILITY
         AIF   (NOT &XA).X002                               *HMD 10/85*
         GETMAIN RU,              LV=LENGTH                            &
               LV=PUTSIZE,        STANDARD OS SA PLUS ANY EXTRA        &
               LOC=RES            RELATIVE TO 16MB LINE
         AGO   .X003                                        *HMD 10/85*
.X002    ANOP                                               *HMD 10/85*
         GETMAIN RU,              LV=LENGTH                            &
               LV=PUTSIZE         STANDARD OS SA PLUS ANY EXTRA
.X003    ANOP                                               *HMD 10/85*
         ST    1,8(13)            OUR SAVE AREA POINTER
         ST    13,4(1)            POINTER TO CALLERS SAVE AREA
         LR    13,1               @OUR SAVE AREA
         USING PUTLDS,R13         MAP SA AND WORK AREA
         L     R14,4(R13)         POINT TO CALLERS RSA
         LM    R0,R1,20(R14)      RESTORE REGS CHANGED BY GETMAIN
         SPACE 2
***********************************************************************
*  UNLOAD ARGUMENTS AND VALIDATE WHERE POSSIBLE
***********************************************************************
         SPACE 1
         LA    R15,RCBA                 ASSUME BAD ADDRESS
         LTR   RMA,R1                   MESSAGE ADDRESS
         BNP   RETURN                   GOOD ASSUMPTION
         LA    R15,RCBL                 ASSUME BAD LENGTH
         LTR   RML,R0                   POSITIVE?
         BNP   RETURN                   -NO
         C     RML,=A(MAXMSG)           MORE THAN MAX?
         BH    RETURN                   -YES
         SPACE 2
***********************************************************************
*  BUILD PARAMETER LIST
***********************************************************************
         SPACE 1
         LA    R15,4(RML)               MESSAGE LENGTH
         STH   R15,PUTLEN                 PLUS HEADER
         XC    PUTZZZ,PUTZZZ            CLEAR 2ND HWORD OF RDW
         BCTR  RML,0                    REDUCE FOR MOVE
         EX    RML,PUTMOVE
         EJECT
***********************************************************************
*  DIG UP ADDRESS OF UPT AND ECT
***********************************************************************
         SPACE 1
         L     R2,PSATOLD-PSA           @TCB                   HD APR88
         L     R2,TCBFSA-TCB(R2)        @1ST PROBLEM PGM SAVE AREA
         L     R2,24(R2)                REG1 THAT WAS PASSED TO CP
         USING CPPL,R2                  MAP CPPL
         L     RUPT,CPPLUPT             @UPT
         L     RECT,CPPLECT             @ECT
         SPACE 2
***********************************************************************
*  ISSUE PUTLINE CALL
***********************************************************************
         SPACE 1
         XC    PUTECB,PUTECB            CLEAR ECB
         XC    PUTBLOK(12),PUTBLOK      CLEAR PARM BLOCK       HD APR88
         PUTLINE PARM=PUTBLOK,          PARAMETER BLOCK                &
               UPT=(RUPT),              @UPT                           &
               ECT=(RECT),              @ECT                           &
               ECB=PUTECB,              @ECB                           &
               OUTPUT=(PUTLEN,TERM,SINGLE,DATA),                       &
               MF=(E,PUTIOPL)
         EJECT
***********************************************************************
*  RETURN TO CALLER
***********************************************************************
         SPACE 1
RETURN   EQU   *
         LR    2,R13                    @OUR SAVE AREA
         LR    3,R15                    SAVE RETURN CODE
         L     R13,4(R13)               @CALLERS SAVE AREA
         FREEMAIN RU,LV=PUTSIZE,A=(R2),SP=0
         LR    R15,3                    RESTORE RC
         RETURN (14,12),RC=(15)         RESTORE CALLERS REGS
         SPACE 5
***********************************************************************
*  CONSTANTS AND NON-REENTRANT WORK AREAS
***********************************************************************
         SPACE 1
PUTMOVE  MVC   PUTMSG(*-*),0(RMA)       MOVE MESSAGE TO BUFFER
         SPACE 2
         LTORG
         SPACE 2
         END   EPUTL
